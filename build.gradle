plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

group 'com.candle'
version '0.1'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.2'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.candle.fileexplorer'
    mainClass = 'com.candle.fileexplorer.StartFilesApp'
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    // Tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation "org.testfx:testfx-core:4.0.16-alpha"
    testImplementation 'org.testfx:testfx-junit5:4.0.16-alpha'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}


run {
    jvmArgs = [
            '--add-exports=javafx.graphics/com.sun.javafx.application=org.testfx',
    ]
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}